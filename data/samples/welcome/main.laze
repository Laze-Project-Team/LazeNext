// Lazeの世界へようこそ！
// Lazeを使えば誰でも簡単にプログラミングができちゃいます

// ここからはLazeの基礎を解説していきます
// もっと詳しく知りたい人は https://laze.ddns.net/docs を読んでね

// 「コンパイル / 実行」ボタンを押せばこのプログラムを実行できます
// 他のサンプルは上のボタンから読み込めます

// 後で使う数学的な関数を定義
関数:f (実数:x) => (実数:y) {
	// y = 2x² - 5x + 2
	y = 2 * x*x - 5 * x + 2;
}

// "実行"という名前の関数を定義してます
// プログラムはここから実行されます
関数:実行 () => () {

	// 例えばこんな感じで変数の宣言ができたり
	実数:変数1 = 10;
	実数:変数2 = 3.14;

	// 演算子を使えば計算も
	表示( 変数1 + 変数2 ); // 11.14
	表示( 変数1 * 変数2 + 3 ); // 28.12

	// 条件分岐や
	もし ( 変数1 > 変数2 ) ならば {
		表示(1); // 1が表示される
	} でなければ {
		表示(0); // 表示されない
	}

	// 繰り返しもできちゃいます
	(10) 回繰り返す {
		表示(カウンタ); // 0～9が表示される
	}

	// 上で定義した数学的な関数を呼び出してみる
	表示(f(0)); // 2
	表示(f(1)); // -1
	表示(f(2)); // 0

	// 文字列の表示は今のところできません...ごめんなさい...

}
